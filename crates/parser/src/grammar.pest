WHITESPACE = _{
    " " | "\t" | NEWLINE
}

COMMENT = _{
    "//" ~ (!NEWLINE ~ ANY) + ~ NEWLINE
}

identifier = @{
    (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")*
}

number = @{
    "0x" ~ ASCII_HEX_DIGIT ~ ("." ~ ASCII_HEX_DIGIT){,1}
    | ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+){,1}
}

// Careful here: we don't want to allow `programname` only `program name`
program_decl = ${
    "program" ~ (WHITESPACE | COMMENT)+ ~ identifier ~ (WHITESPACE | COMMENT)* ~ ";"
}

meta_obj = {
    "{" ~ (identifier ~ ":" ~ meta_val ~ ("," | &"}"))* ~ "}"
}

meta_array = {
    "[" ~ (meta_val ~ ("," | &"]")) * ~ "]"
}

meta_literal = {
    identifier | number
}

meta_val = {
    meta_obj | meta_array | meta_literal
}

external_block = {
    "external" ~ meta_obj
}

program = {
    SOI ~ program_decl ~ external_block ~ EOI
}
